on quit
	try
		tell application "System Events" to tell process "Deluge"
			activate
			set frontmost to true
			windows where title contains "Add Torrents"
			if result is not {} then perform action "AXRaise" of item 1 of result
		end tell
	end try
end quit

on open location this_URL
	try
		tell application "Finder" to set delugePath to POSIX path of (application file id "org.deluge" as string)
		set appExists to true
	on error
		set appExists to false
		display alert "Deluge.app must be installed in order to use this plug-in." as critical
		return
	end try
	set delugeArgument to quoted form of this_URL
	try
		tell application "Deluge" to activate
	end try
	do shell script (quoted form of (delugePath & "/Contents/MacOS/Deluge") & " add " & delugeArgument & "; return;")
	quit
end open location
tell application "Finder" to set thisPath to (POSIX path of (application file id "org.deluge.MagnetURIHandler" as string))
log thisPath
set check_default to do shell script "MAGNET_DEFAULT_APP=`VERSIONER_PERL_PREFER_32_BIT=1 perl -MMac::InternetConfig -le \"print +(GetICHelper 'magnet')[1]\"`; if [[ $MAGNET_DEFAULT_APP = 'Magnet Handler' ]]; then echo TRUE; else echo FALSE; fi"
if (check_default = "TRUE") then
	display dialog "Magnet Handler is already the default application to handle magnet URIs" buttons {"OK"} with title "Notification" with icon POSIX file (thisPath & "/Contents/Resources/deluge_magnet.icns")
else
	try
		set dialogResult to display dialog "Magnet Handler has not been configured to handle magnet URIs.

Would you like to do this now?" buttons {"Yes", "No"} default button "Yes" cancel button "No" with title "Notification" with icon POSIX file (thisPath & "/Contents/Resources/deluge_magnet.icns")
	on error number -128
	end try
	try
		if button returned of dialogResult is "Yes" then
			set dutiPath to quoted form of (thisPath & "/Contents/Resources/duti") & " -s org.deluge.MagnetURIHandler magnet all && /System/Library/Frameworks/CoreServices.framework/Versions/A/Frameworks/LaunchServices.framework/Versions/A/Support/lsregister -kill -r -domain local -domain system -domain user"
			log dutiPath
			do shell script dutiPath
			
		end if
	end try
end if
quit